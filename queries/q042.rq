PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bd: <http://www.bigdata.com/rdf#>
# -------------------------------------
#defaultView:AreaChart
SELECT ?cid (STR(SAMPLE(?year)) AS ?year) (COUNT(*) AS ?count) ?cause WHERE {
  ?pid wdt:P31 wd:Q5;  # instance of human
       wdt:P509 ?cid;  # cause of death
       wdt:P570 ?_date_of_death.
  ?cid wdt:P279* wd:Q12078.  # type of cancer
 SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en".
                         ?cid rdfs:label ?cause. }
  BIND ( YEAR(?_date_of_death) AS ?year )
  FILTER( ?year > 1960 )
}
GROUP BY ?cid ?cause ?year
